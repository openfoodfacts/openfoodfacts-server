# This file is part of Product Opener.
#
# Product Opener
# Copyright (C) 2011-2020 Association Open Food Facts
# Contact: contact@openfoodfacts.org
# Address: 21 rue des Iles, 94100 Saint-Maur des Foss√©s, France
#
# Product Opener is free software: you can redistribute it and/or modify
# it under the terms of the GNU Affero General Public License as
# published by the Free Software Foundation, either version 3 of the
# License, or (at your option) any later version.
#
# This program is distributed in the hope that it will be useful,
# but WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
# GNU Affero General Public License for more details.
#
# You should have received a copy of the GNU Affero General Public License
# along with this program.  If not, see <http://www.gnu.org/licenses/>.

=head1 NAME

ProductOpener::APITest - utility functions to interact with API

=head1 DESCRIPTION

=cut

package ProductOpener::APITest;

use ProductOpener::PerlStandards;
use Exporter qw< import >;

BEGIN {
	use vars qw(@ISA @EXPORT_OK %EXPORT_TAGS);
	@EXPORT_OK = qw(
	  &create_user
	  &new_client
	  &wait_dynamic_front
	);    # symbols to export on request
	%EXPORT_TAGS = (all => [@EXPORT_OK]);
}

use vars @EXPORT_OK;

use LWP::UserAgent;
use HTTP::CookieJar::LWP;

use Data::Dump qw/dump/;

=head2 wait_dynamic_front()

Wait for dynamic_front to be ready.
It's important because the application might fail because of that

=cut

sub wait_dynamic_front() {

	# simply try to access a resource generated by dynamicfront
	my $count = 0;
	while (1) {
		last if (-e "/opt/product-opener/html/images/icons/dist/barcode.svg");
		sleep 1;
		$count++;
		if (($count % 3) == 0) {
			print("Wainting for dynamicfront to be ready since $count seconds...\n");
		}
	}
	return;
}

=head2 new_client()

Reset user agent

=head3 return value

Return a user agent

=cut

sub new_client () {
	my $jar = HTTP::CookieJar::LWP->new;
	my $ua = LWP::UserAgent->new(cookie_jar => $jar);
	return $ua;
}

=head2 create_user($ua, $args_ref)

Call API to create a user

=head3 Arguments

=head4 $ua - user agent

=head4 $args_ref - optional args to override defaults

=cut

sub create_user ($ua, $args_ref) {
	my %fields = (
		email => 'test@test.com',
		userid => "test",
		name => "Test",
		password => "testtest",
		confirm_password => "testtest",
		pro_checkbox => 0,
		requested_org => "",
		team_1 => "",
		team_2 => "",
		team_3 => "",
		action => "process",
		type => "add",
		".submit" => "Register"
	);

	# apply overrides
	while (my ($key, $value) = each %{$args_ref}) {
		$fields{$key} = $value;
	}
	my $response = $ua->post("http://world.openfoodfacts.localhost/cgi/user.pl", Content => \%fields,);
	$response->is_success or die("Couldn't create user with " . dump(\%fields) . "\n");
	return;
}

sub create_product ($ua, $args_ref) {
	
	my %fields = (
		code => "2000000000099",
		lang => "en",
		product_name => "Test-75ml",
		generic_name => "Tester",
		quantity => "75 ml",
		link => "https://github.com/openfoodfacts/openfoodfacts-server",
		expiration_date => "test"
		ingredients_text => "apple, milk",
		origin => "france",
		serving_size => "10g",
		packaging_text => "no",
		action => "process",
		type => "add",
		".submit" => "Save"
	);

	# overrides
	while (my ($key, $value) = each %{$args_ref}) {
		$fields{$key} = $value;
	}
	my $response = $ua->post("http://world.openfoodfacts.localhost/cgi/product.pl", Content => \%fields,);
	$response->is_success or die("Couldn't create product with " . dump(\%fields) . "\n");
}

1;
